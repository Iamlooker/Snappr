plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.dagger.hilt.android'
    id "com.google.devtools.ksp"
}

android {
    compileSdk 34
    buildToolsVersion = "34.0.0"
    namespace = "com.looker.notesy"
    defaultConfig {
        applicationId "com.looker.notesy"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix = ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += "-Xcontext-receivers"
    }
    buildFeatures {
        compose true
    }
    composeCompiler {
        enableStrongSkippingMode = true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def composeBom = platform('dev.chrisbanes.compose:compose-bom:2024.06.00-alpha01')
    def ktorBom = platform('io.ktor:ktor-bom:2.3.12')
    implementation composeBom
    implementation ktorBom
    androidTestImplementation composeBom

    // Core
    implementation('androidx.core:core-ktx:1.13.1')
    implementation('androidx.core:core-splashscreen:1.0.1')
    implementation('androidx.activity:activity-compose:1.9.0')

    // Coil
    implementation('io.coil-kt:coil-compose:2.6.0')

    // Compose
    implementation('androidx.compose.runtime:runtime')
    implementation('androidx.compose.ui:ui')
    implementation('androidx.compose.foundation:foundation')
    implementation('androidx.compose.material:material-icons-extended')
    implementation('androidx.compose.material3:material3')
    implementation('androidx.compose.ui:ui-text-google-fonts')
    implementation('androidx.compose.ui:ui-tooling-preview')

    // Coroutines
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0-RC')
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0-RC')
    implementation('org.jetbrains.kotlinx:kotlinx-datetime:0.6.0')

    // Datastore
    implementation('androidx.datastore:datastore-preferences:1.1.1')

    // Hilt
    implementation('androidx.hilt:hilt-navigation-compose:1.2.0')
    implementation('com.google.dagger:hilt-android:2.51.1')
    ksp('com.google.dagger:hilt-compiler:2.51.1')

    // Jsoup
    implementation('org.jsoup:jsoup:1.16.1')

    // Ktor
    implementation('io.ktor:ktor-client-core')
    implementation('io.ktor:ktor-client-okhttp')
    implementation('io.ktor:ktor-client-logging')

    // Lifecycle
    implementation('androidx.lifecycle:lifecycle-runtime-ktx:2.8.3')
    implementation('androidx.lifecycle:lifecycle-runtime-compose:2.8.3')
    implementation('androidx.lifecycle:lifecycle-viewmodel-compose:2.8.3')

    // Navigation
    implementation('androidx.navigation:navigation-compose:2.7.7')

    // Palette
    implementation('androidx.palette:palette-ktx:1.0.0')

    // Room
    implementation('androidx.room:room-runtime:2.6.1')
    implementation('androidx.room:room-ktx:2.6.1')
    ksp('androidx.room:room-compiler:2.6.1')

    // Test
    testImplementation('junit:junit:4.13.2')
    testImplementation('com.google.truth:truth:1.1.5')
    androidTestImplementation('androidx.test.ext:junit:1.2.1')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.6.1')
    androidTestImplementation('androidx.compose.ui:ui-test-junit4')
    debugImplementation('androidx.compose.ui:ui-tooling')
    debugImplementation('androidx.compose.ui:ui-test-manifest')
}